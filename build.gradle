buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'com.jfrog.bintray' version "1.8.1"
    id 'scala'
    id 'maven-publish'
    id "com.github.maiflai.scalatest" version "0.25"
}

ext {
    latestScalaFull = [
        '2.11': '2.11.12',
        '2.12': '2.12.10',
        '2.13': '2.13.1'
    ]

    vScalaTest = '3.0.8'
    vPegdown = '1.6.0'
    vScala = System.properties.scalaVersion ?: '2.13'
    vScalaFull = latestScalaFull[vScala]
    vHazelcast = '3.12.3'
    vKryo = '4.0.2'
    vObjenesis = '3.1'
    vLz4 = '1.6.0'
}

allprojects {
    apply plugin: 'scala'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    dependencies {

        compile "javax.cache:cache-api:1.0.0"
        compile "javax.transaction:transaction-api:1.1"

        compileOnly "org.scala-lang:scala-reflect:$vScalaFull"
        if (vScala == '2.13') {
            compileOnly "org.scala-lang.modules:scala-parallel-collections_$vScala:0.2.0"
            testCompile "org.scala-lang.modules:scala-parallel-collections_$vScala:0.2.0"
        }

        compileOnly "com.hazelcast:hazelcast:$vHazelcast"
        compileOnly "com.hazelcast:hazelcast-client:$vHazelcast"

        compileOnly "com.esotericsoftware:kryo:$vKryo"
        compileOnly "org.objenesis:objenesis:$vObjenesis"

        compileOnly "org.lz4:lz4-java:$vLz4"

        testCompile "org.scalatest:scalatest_$vScala:$vScalaTest"
        testCompile "org.pegdown:pegdown:$vPegdown"
        testCompile "org.scala-lang:scala-compiler:$vScalaFull"
        testCompile "com.hazelcast:hazelcast:$vHazelcast"
        testCompile "com.hazelcast:hazelcast-client:$vHazelcast"
        testCompile "com.esotericsoftware:kryo:$vKryo"
        testCompile "org.objenesis:objenesis:$vObjenesis"
        testCompile "org.lz4:lz4-java:$vLz4"

    }

}

tasks.withType(ScalaCompile) {
    def args = System.properties.compilerArgs ?: "-feature -deprecation -explaintypes -unchecked -Xlint"
    scalaCompileOptions.additionalParameters = args.split(" ").toList()
}
tasks.withType(Jar) {
    baseName = "${project.name}_$vScala"
}

test {
    maxParallelForks = 1
}

sourceCompatibility = 1.7
if (vScala == '2.11') {
    targetCompatibility = 1.7
} else {
    targetCompatibility = 1.8
}

sourceSets {
    def diff = (vScala == '2.11' || vScala == '2.12') ? 'src/main/legacy' : 'src/main/modern'
    main {
        scala {
            srcDirs = ['src/main/scala', diff]
        }
    }
}

wrapper {
    gradleVersion = '5.6.2'
}

if (version == '') {
    version = 'SNAPSHOT'
}
if (version == 'SNAPSHOT') {
    version = tsVersion(version)
}

def tsVersion(version) {
    java.text.SimpleDateFormat tsFmt = new java.text.SimpleDateFormat('yyMMddHHmm')
    tsFmt.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')))
    return "${version}-b${tsFmt.format(new Date())}"
}

repositories {
    jcenter()
}

jar {
    baseName = "${project.name}_$vScala"
    manifest {
        attributes "Implementation-Title": project.name, "Implementation-Version": version
    }
}

task sourceJar(type: Jar) {
    baseName "${project.name}_$vScala"
    classifier "sources"
    from sourceSets.main.allScala
}
task docsJar(type: Jar, dependsOn: scaladoc) {
    baseName "${project.name}_$vScala"
    classifier "docs"
    from scaladoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.hazelcast'
            artifactId "${project.name}_$vScala"

            from components.java
            artifact sourceJar
// FIXME    artifact docsJar
        }
    }
}

bintray {
    user = System.properties.bintrayUser ?: 'dryrun'
    key = System.properties.bintrayApiKey ?: 'dryrun'
    dryRun = (user == 'dryrun' || key == 'dryrun' || version.startsWith('SNAPSHOT'))
    publish = !dryRun
    publications = ['maven']
    pkg {
        userOrg = System.properties.bintrayOrg
        repo = System.properties.bintrayRepo
        name = 'hazelcast-scala'
    }
}
